<program>          -> <statement_list>

<statement_list>   -> <statement> <statement_list>
                    | ε

<statement>        -> <variable_declaration>
                    | <object_declaration>
                    | <function_definition>
                    | <property_declaration>
                    | <uniform_declaration>
                    | <expression_statement>
                    | <comment>

<comment>          -> "$" <COMMENT_TEXT>
<COMMENT_TEXT>     -> .*  # everything after $$ on the line

# Properties and uniforms
<property_declaration> -> "@property" "(" <string_literal> "," <type> ")" ";"
<uniform_declaration>  -> "@uniform" "(" <identifier> ")" ";"

# Variable assignment
<variable_declaration> -> <identifier> "=" <expression> ";"

# Object declaration
<object_declaration>   -> <identifier> "=" "{" <object_body> "}" ";"
<object_body>          -> <object_member> <object_body>
                        | ε
<object_member>        -> <identifier> ":" <type_or_expression>
                        | <identifier> ":" <type_or_expression> "=" <expression>

# Function definition
<function_definition> -> "fn" <identifier> "(" <param_list> ")" "{" <statement_list> "}" ";"
<param_list>          -> <param> "," <param_list>
                       | <param>
                       | ε
<param>               -> <type> <identifier>
                       | <identifier>           # for type inference

# Expressions
<expression_statement> -> <expression> ";"

<expression>          -> <string_literal>
                        | <number_literal>
                        | <identifier>
                        | <function_call>
                        | <object_access>
                        | <binary_expression>
                        | <parenthesized_expression>

<function_call>       -> <identifier> "(" <arg_list> ")"
<arg_list>            -> <expression> "," <arg_list>
                       | <expression>
                       | ε

<object_access>       -> <identifier> "." <identifier>

<binary_expression>   -> <expression> <operator> <expression>
<operator>            -> "+" | "-" | "*" | "/" | "%" | "="

<parenthesized_expression> -> "(" <expression> ")"

# Terminals
<identifier>          -> [a-zA-Z_][a-zA-Z0-9_]*
<string_literal>      -> "\"" .* "\""
<number_literal>      -> [0-9]+(\.[0-9]+)?
<type>                -> "Vector2" | "Vector3" | "Vector4" | "Color" | "SamplerState" | "float" | "int" | "Texture2D" | "float2" | "float3" | "float4"
<type_or_expression>  -> <type> | <expression>